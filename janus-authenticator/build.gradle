apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'
apply plugin: 'com.novoda.bintray-release'

def libArtifactId = 'janus'
def libGroupId = 'com.praszapps.biometric'


publish {
    userOrg = 'prasannajeet89'
    repoName = 'praszappsMaven'
    groupId = libGroupId
    artifactId = libArtifactId
    publishVersion = generateVersionName()
    desc = 'Janus - The easy biometric authentication library for Android'
    website = 'https://github.com/prasannajeet/Janus'
    issueTracker = 'https://github.com/prasannajeet/Janus/issues'
    repository = 'https://github.com/prasannajeet/Janus.git'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion rootProject.ext.minimumSdkVersion
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    lintOptions {
        abortOnError false
    }
    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return rootProject.ext.minimumSdkVersion * 10000000 + rootProject.ext.versionMajor * 10000 + rootProject.ext.versionMinor * 100 + rootProject.ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${rootProject.ext.versionMajor}.${rootProject.ext.versionMinor}.${rootProject.ext.versionPatch}"

    return versionName
}

publishing {
    publications {
        maven(MavenPublication) {

            setGroupId(libGroupId)
            setArtifactId(libArtifactId)
            setVersion(android.defaultConfig.versionName)
            project.logger.lifecycle("I am in publications")
            artifact(bundleRelease)
            pom.withXml {
                project.logger.lifecycle("I am in withXml")
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if ("unspecified" != it.name) {
                        project.logger.lifecycle("Dependency addded - "+it.group+":"+it.name+":"+it.version)
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$rootProject.ext.androidSupportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.constraintLayoutVersion"
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.testRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressionVersion"
    implementation "com.android.support:design:$rootProject.ext.androidSupportLibVersion"
    /*api "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
        kapt "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
        api "com.google.dagger:dagger-android-support:$rootProject.ext.daggerVersion" // if you use the support libraries
        kapt "com.google.dagger:dagger-android-processor:$rootProject.ext.daggerVersion"*/
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // ReactiveX
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.26.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.26.0'
}